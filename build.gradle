/*
 * Copyright 2020 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "application"
    id "com.jfrog.artifactory" version "4.31.9"
    id "java"
    id "checkstyle"
    id "io.spring.javaformat" version "0.0.38"
    id "maven-publish"
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id "io.spring.nohttp" version "0.0.11"
}

apply from: "gradle/artifactory.gradle"

group "io.spring.autoln"

ext {
    springJavaFormatCheckstyleVersion = io.spring.javaformat.gradle.SpringJavaFormatPlugin.class.getPackage().getImplementationVersion()
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "info.picocli:picocli-codegen:4.5.1"
    checkstyle "io.spring.javaformat:spring-javaformat-checkstyle:0.0.25"
    implementation "info.picocli:picocli:4.5.1"
    testImplementation platform("org.junit:junit-bom:5.6.2")
    testImplementation "org.jsoup:jsoup:1.13.1"
    testImplementation "org.assertj:assertj-core:3.15.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

application {
    mainClass = "io.spring.autoln.AutolnCommand"
}

run {
    if (project.hasProperty("args")) {
        args += project.args
    }
}

ext {
    mainClassName = "io.spring.autoln.AutolnCommand"
}

task generateManpageAsciiDoc(type: JavaExec) {
    dependsOn(classes)
    group = "Documentation"
    description = "Generate AsciiDoc manpage"
    classpath(configurations.compileClasspath, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    main 'picocli.codegen.docgen.manpage.ManPageGenerator'
    args project.ext.mainClassName, "--outdir=${project.buildDir}/generated-picocli-docs", "-v", "--template-dir=src/docs/mantemplates"
}

asciidoctor {
    sourceDir file("${project.buildDir}/generated-picocli-docs")
    outputDir file("${project.buildDir}/docs")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}